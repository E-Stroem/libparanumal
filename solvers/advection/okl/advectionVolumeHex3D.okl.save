/*

  The MIT License (MIT)

  Copyright (c) 2017 Tim Warburton, Noel Chalmers, Jesse Chan, Ali Karakus

  Permission is hereby granted, free of charge, to any person obtaining a copy
  of this software and associated documentation files (the "Software"), to deal
  in the Software without restriction, including without limitation the rights
  to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
  copies of the Software, and to permit persons to whom the Software is
  furnished to do so, subject to the following conditions:

  The above copyright notice and this permission notice shall be included in all
  copies or substantial portions of the Software.

  THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
  IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
  FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
  AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
  LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
  OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
  SOFTWARE.

*/


// isotropic advection
@kernel void advectionVolumeHex3D(const dlong Nelements,
				  @restrict const dfloat * vgeo,
				  @restrict const dfloat * D,
				  @restrict const dfloat * advectionVelocityJW,
				  @restrict const dfloat * q,
				  @restrict dfloat * rhsq){
  
  // each thread-block outputs p_NblockAdvVol elements
  for(dlong eo=0;eo<Nelements;eo+=p_NblockAdvVol;@outer(0)){

    // collocation differentiation
    @shared dfloat s_D[p_Nq][p_Nq];
    
    // use 2*Nq^2 shared and 2*Nq*Nq*Nq registers 
    // [ 3D shared would required 3*Nq*Nq*Nq shared ]
    
    @shared dfloat s_F[p_NblockAdvVol][p_Nq][p_Nq];
    @shared dfloat s_G[p_NblockAdvVol][p_Nq][p_Nq];

    @exclusive dfloat r_q[p_NblockAdvVol][p_Nq];
    @exclusive dfloat r_Aq[p_NblockAdvVol][p_Nq];
    @exclusive dfloat r_Hk[p_NblockAdvVol];

    for(int j=0;j<p_Nq;++j;@inner(1)){
      for(int i=0;i<p_Nq;++i;@inner(0)){
	
#pragma unroll p_NblockAdvVol
	for(int es=0;es<p_NblockAdvVol;++es){
	  
	  const dlong e = eo+es;
	  if(e<Nelements){

#pragma unroll p_Nq
	    for(int k=0;k<p_Nq;++k){
	      const dlong  qbase = e*p_Np*p_Nfields + k*p_Nq*p_Nq + j*p_Nq + i;
	      
	      r_q[es][k] = q[qbase+0*p_Np];
	    }
	  }

#pragma unroll p_Nq
	  for(int k=0;k<p_Nq;++k){
	    r_Aq[es][k] = 0;
	  }
	}

	s_D[j][i] = D[j*p_Nq+i];
      }
    }
    
#pragma unroll p_Nq
    for(int k=0;k<p_Nq;++k){
      
      @barrier("local");
      
      for(int j=0;j<p_Nq;++j;@inner(1)){
        for(int i=0;i<p_Nq;++i;@inner(0)){
	  
#pragma unroll p_NblockAdvVol
	  for(int es=0;es<p_NblockAdvVol;++es){
	    
	    const dlong e = eo+es;
	    if(e<Nelements){
	      // geometric factors
	      const dlong gbase = e*p_Np*p_dim + k*p_Nq*p_Nq + j*p_Nq + i;

	      // J*W*c_x, J*W*c_y, J*W*c_z
	      const dfloat Fr = advectionVelocityJW[gbase+p_Np*0];
	      const dfloat Fs = advectionVelocityJW[gbase+p_Np*1];
	      const dfloat Ft = advectionVelocityJW[gbase+p_Np*2];
	      
	      const dfloat qn = r_q[es][k];
	      
	      s_F[es][j][i] = Fr*qn;
	      s_G[es][j][i] = Fs*qn;
	      r_Hk[es]      = Ft*qn;
	    }
	  }
	}
      }
      
      @barrier("local");
      
      for(int j=0;j<p_Nq;++j;@inner(1)){
        for(int i=0;i<p_Nq;++i;@inner(0)){    

#pragma unroll p_Nq          
          for(int n=0;n<p_Nq;++n){
	    // read D^t
            const dfloat Dni = s_D[n][i]; 
            const dfloat Dnj = s_D[n][j];
            const dfloat Dkn = s_D[k][n];

	    // 6*p_NblockAdvVol flops for 8*3 + p_NblockAdvVol*2*8 bytes from shared

#pragma unroll p_NblockAdvVol
	    for(int es=0;es<p_NblockAdvVol;++es){	    
	      r_Aq[es][k] += Dni*s_F[es][j][n];
	      r_Aq[es][k] += Dnj*s_G[es][n][i];
	      r_Aq[es][n] += Dkn*r_Hk[es]; // can move this up
	    }
          }
	}
      }
    }

    for(int j=0;j<p_Nq;++j;@inner(1)){
      for(int i=0;i<p_Nq;++i;@inner(0)){    

#pragma unroll p_NblockAdvVol
	for(int es=0;es<p_NblockAdvVol;++es){
	  
	  const dlong e = eo+es;
	  if(e<Nelements){

#pragma unroll p_Nq 	    
	    for(int k=0;k<p_Nq;++k){    
	      const dlong gid = e*p_Np*p_Nvgeo+ k*p_Nq*p_Nq + j*p_Nq +i;
	      const dfloat invJW = vgeo[gid + p_IJWID*p_Np];
	      
	      const dlong base = e*p_Np*p_Nfields + k*p_Nq*p_Nq + j*p_Nq + i;
	      
	      // move to rhs   (dq/dt = div (c*q) => -div'V*M*c*q
	      rhsq[base+0*p_Np] = -invJW*r_Aq[es][k];
	    }
	  }
	}
      }
    }
  }
}




@kernel void advectionSkewVolumeHex3D(const dlong Nelements,
				      @restrict const dfloat * vgeo,
				      @restrict const dfloat * D,
				      @restrict const dfloat * advectionVelocityJW,
				      @restrict const dfloat * q,
				      @restrict dfloat * rhsq){
  
  // each thread-block outputs p_NblockAdvVol elements
  for(dlong eo=0;eo<Nelements;eo+=p_NblockAdvVol;@outer(0)){

    // collocation differentiation
    @shared dfloat s_D[p_Nq][p_Nq];
   
    
    // use 2*Nq^2 shared and 2*Nq*Nq*Nq registers 
    // [ 3D shared would required 3*Nq*Nq*Nq shared ]
    
    @shared dfloat s_F[p_NblockAdvVol][p_Nq][p_Nq];
    @shared dfloat s_G[p_NblockAdvVol][p_Nq][p_Nq];
    @shared dfloat s_q[p_NblockAdvVol][p_Nq][p_Nq];

    @exclusive dfloat r_q[p_NblockAdvVol][p_Nq];
    @exclusive dfloat r_Aq[p_NblockAdvVol][p_Nq];
    @exclusive dfloat r_Hk[p_NblockAdvVol];
    @exclusive dfloat r_Fr[p_NblockAdvVol];
    @exclusive dfloat r_Fs[p_NblockAdvVol];
    @exclusive dfloat r_Ft[p_NblockAdvVol];

    for(int j=0;j<p_Nq;++j;@inner(1)){
      for(int i=0;i<p_Nq;++i;@inner(0)){
	
#pragma unroll p_NblockAdvVol
	for(int es=0;es<p_NblockAdvVol;++es){
	  
	  const dlong e = eo+es;
	  if(e<Nelements){

#pragma unroll p_Nq
	    for(int k=0;k<p_Nq;++k){
	      const dlong  qbase = e*p_Np*p_Nfields + k*p_Nq*p_Nq + j*p_Nq + i;
	      
	      r_q[es][k] = q[qbase+0*p_Np];
	    }
	  }

#pragma unroll p_Nq
	  for(int k=0;k<p_Nq;++k){
	    r_Aq[es][k] = 0;
	  }
	}

	s_D[j][i] = D[j*p_Nq+i];
      }
    }
    
#pragma unroll p_Nq
    for(int k=0;k<p_Nq;++k){
      
      @barrier("local");
      
      for(int j=0;j<p_Nq;++j;@inner(1)){
        for(int i=0;i<p_Nq;++i;@inner(0)){
	  
#pragma unroll p_NblockAdvVol
	  for(int es=0;es<p_NblockAdvVol;++es){
	    
	    const dlong e = eo+es;
	    if(e<Nelements){
	      // geometric factors
	      const dlong gbase = e*p_Np*p_dim + k*p_Nq*p_Nq + j*p_Nq + i;

	      // J*W*c_x, J*W*c_y, J*W*c_z
	      r_Fr[es] = advectionVelocityJW[gbase+p_Np*0];
	      r_Fs[es] = advectionVelocityJW[gbase+p_Np*1];
	      r_Ft[es] = advectionVelocityJW[gbase+p_Np*2];
	      
	      const dfloat qn = r_q[es][k];

	      s_q[es][j][i] = qn;
	      s_F[es][j][i] = r_Fr[es]*qn;
	      s_G[es][j][i] = r_Fs[es]*qn;
	      r_Hk[es]      = r_Ft[es]*qn;
	    }
	  }
	}
      }
      
      @barrier("local");
      
      for(int j=0;j<p_Nq;++j;@inner(1)){
        for(int i=0;i<p_Nq;++i;@inner(0)){    

#pragma unroll p_Nq          
          for(int n=0;n<p_Nq;++n){
	    // read D^t
            const dfloat Dni = s_D[n][i]; 
            const dfloat Dnj = s_D[n][j];
            const dfloat Dkn = s_D[k][n];
	    const dfloat Din = s_D[i][n]; 
	    const dfloat Djn = s_D[j][n];

	    // 6*p_NblockAdvVol flops for 8*3 + p_NblockAdvVol*2*8 bytes from shared

#pragma unroll p_NblockAdvVol
	    for(int es=0;es<p_NblockAdvVol;++es){
	      // MISSING FACTOR OF 0.5 (added below)
	      r_Aq[es][k] += Dni*s_F[es][j][n];
	      r_Aq[es][k] += Dnj*s_G[es][n][i];
	      // THIS FEELS WRONG
	      r_Aq[es][n] += Dkn*r_Hk[es]; // can move this up

	      r_Aq[es][k] -= r_Fr[es]*Din*s_q[es][j][n];
	      r_Aq[es][k] -= r_Fs[es]*Djn*s_q[es][n][i];
	      r_Aq[es][k] -= r_Ft[es]*Dkn*r_q[es][n];
	    }
	  }
	}
      }
    }

    for(int j=0;j<p_Nq;++j;@inner(1)){
      for(int i=0;i<p_Nq;++i;@inner(0)){    

#pragma unroll p_NblockAdvVol
	for(int es=0;es<p_NblockAdvVol;++es){
	  
	  const dlong e = eo+es;
	  if(e<Nelements){

#pragma unroll p_Nq 	    
	    for(int k=0;k<p_Nq;++k){    
	      const dlong gid = e*p_Np*p_Nvgeo+ k*p_Nq*p_Nq + j*p_Nq +i;
	      const dfloat invJW = vgeo[gid + p_IJWID*p_Np];
	      
	      const dlong base = e*p_Np*p_Nfields + k*p_Nq*p_Nq + j*p_Nq + i;
	      
	      // move to rhs   (dq/dt = div (c*q) => -div'V*M*c*q
	      rhsq[base+0*p_Np] = -p_half*invJW*r_Aq[es][k];
	    }
	  }
	}
      }
    }
  }
}




@kernel void advectionCombinedHex3D(const dlong Nelements,
				    const dfloat dt,
				    const dfloat rka,
				    const dfloat rkb,
				    @restrict const dfloat * vgeo,
				    @restrict const dfloat * D,
				    @restrict const dfloat * advectionVelocityJW,
				    @restrict const dlong  * vmapM,
				    @restrict const dlong  * vmapP,
				    @restrict const dfloat * advectionVelocityM,
				    @restrict const dfloat * advectionVelocityP,
				    @restrict dfloat * resq,
				    @restrict const dfloat * q,
				    @restrict dfloat * qnew){
  
  // each thread-block outputs p_NblockAdvVol elements
  for(dlong eo=0;eo<Nelements;eo+=p_NblockAdvVol;@outer(0)){
    
    // collocation differentiation
    @shared dfloat s_D[p_Nq][p_Nq];
    
    // use 2*Nq^2 shared and 2*Nq*Nq*Nq registers 
    // [ 3D shared would required 3*Nq*Nq*Nq shared ]
    
    @shared dfloat s_F[p_NblockAdvVol][p_Nq][p_Nq];
    @shared dfloat s_G[p_NblockAdvVol][p_Nq][p_Nq];

    @exclusive dfloat r_q[p_NblockAdvVol][p_Nq];
    @exclusive dfloat r_Aq[p_NblockAdvVol][p_Nq];
    @exclusive dfloat r_Hk[p_NblockAdvVol];

    for(int j=0;j<p_Nq;++j;@inner(1)){
      for(int i=0;i<p_Nq;++i;@inner(0)){
	
#pragma unroll p_NblockAdvVol
	for(int es=0;es<p_NblockAdvVol;++es){
	  
	  const dlong e = eo+es;
	  if(e<Nelements){

#pragma unroll p_Nq
	    for(int k=0;k<p_Nq;++k){
	      const dlong  qbase = e*p_Np*p_Nfields + k*p_Nq*p_Nq + j*p_Nq + i;
	      
	      r_q[es][k] = q[qbase+0*p_Np];
	    }
	  }

#pragma unroll p_Nq
	  for(int k=0;k<p_Nq;++k){
	    r_Aq[es][k] = 0;
	  }
	}

	s_D[j][i] = D[j*p_Nq+i];
      }
    }
    
#pragma unroll p_Nq
    for(int k=0;k<p_Nq;++k){
      
      @barrier("local");
      
      for(int j=0;j<p_Nq;++j;@inner(1)){
        for(int i=0;i<p_Nq;++i;@inner(0)){
	  
#pragma unroll p_NblockAdvVol
	  for(int es=0;es<p_NblockAdvVol;++es){
	    
	    const dlong e = eo+es;
	    if(e<Nelements){
	      // geometric factors
	      const dlong gbase = e*p_Np*p_dim + k*p_Nq*p_Nq + j*p_Nq + i;

	      // J*W*c_x, J*W*c_y, J*W*c_z
	      const dfloat Fr = advectionVelocityJW[gbase+p_Np*0];
	      const dfloat Fs = advectionVelocityJW[gbase+p_Np*1];
	      const dfloat Ft = advectionVelocityJW[gbase+p_Np*2];
	      
	      const dfloat qn = r_q[es][k];

	      s_F[es][j][i] = Fr*qn;
	      s_G[es][j][i] = Fs*qn;
	      r_Hk[es]      = Ft*qn;
	    }
	  }
	}
      }
      
      @barrier("local");
      
      for(int j=0;j<p_Nq;++j;@inner(1)){
        for(int i=0;i<p_Nq;++i;@inner(0)){    

#pragma unroll p_Nq          
          for(int n=0;n<p_Nq;++n){
	    // read D^t
            const dfloat Dni = s_D[n][i]; 
            const dfloat Dnj = s_D[n][j];
            const dfloat Dkn = s_D[k][n];

	    // 6*p_NblockAdvVol flops for 8*3 + p_NblockAdvVol*2*8 bytes from shared

#pragma unroll p_NblockAdvVol
	    for(int es=0;es<p_NblockAdvVol;++es){
	      // MISSING FACTOR OF 0.5 (added below)
	      r_Aq[es][k] += Dni*s_F[es][j][n];
	      r_Aq[es][k] += Dnj*s_G[es][n][i];
	      r_Aq[es][n] += Dkn*r_Hk[es]; // can move this up

	    }
	  }
	}
      }
    }

    // do faces
    for(int j=0;j<p_Nq;++j;@inner(1)){
      for(int i=0;i<p_Nq;++i;@inner(0)){    

	//#pragma unroll p_NblockAdvVol
	for(int es=0;es<p_NblockAdvVol;++es){
	  
	  const dlong e = eo+es;
	  if(e<Nelements){
	    int k=0;
	    
#pragma unroll p_Nq
	    for(k=0;k<p_Nq;++k){
	      const dlong gid = e*p_Np*p_Nvgeo+ k*p_Nq*p_Nq + j*p_Nq +i;
	      const dfloat invJW = vgeo[gid + p_IJWID*p_Np];
	      r_Aq[es][k] *= -invJW; // divide by mass
	    }

	    
	    dlong id, idM, idP;
	    dfloat FM, FP, qM, qP;
	    
	    // do face 0
	    id = e*p_Nfaces*p_Nfp + 0*p_Nfp + j*p_Nq + i;
	    FM = advectionVelocityM[id]; 
	    FP = advectionVelocityP[id];
	    
	    idM = vmapM[id];
	    idP = vmapP[id];

	    qM = r_q[es][0];
	    qP = (idM!=idP) ? q[idP]: -qM;
	    
	    r_Aq[es][0] += FP*qP + FM*qM;
	    
	    // do face 5
	    id = e*p_Nfaces*p_Nfp + 5*p_Nfp + j*p_Nq + i;
	    FM = advectionVelocityM[id];
	    FP = advectionVelocityP[id];
	    
	    idM = vmapM[id];
	    idP = vmapP[id];

	    qM = r_q[es][p_Nq-1];
	    qP = (idM!=idP) ? q[idP]: -qM;
	    
	    r_Aq[es][p_Nq-1] += FP*qP + FM*qM;
	    
	    // do face 1 && face 3
	    if(j==0 || j==p_Nq-1){
	      int face = (j==0) ? 1:3;

#pragma unroll p_Nq
	      for(k=0;k<p_Nq;++k){
		id = e*p_Nfaces*p_Nfp + face*p_Nfp + k*p_Nq + i;

		FM = advectionVelocityM[id]; 
		FP = advectionVelocityP[id];
		
		idM = vmapM[id];
		idP = vmapP[id];
		
		qM = r_q[es][k];
		qP = (idM!=idP) ? q[idP]: -qM;
		
		r_Aq[es][k] += FP*qP + FM*qM;
	      }
	      
	    }

	
	    // do face 2 && face 4	    
	    if(i==0 || i==p_Nq-1){
	      int face = (i==0) ? 4:2;

#pragma unroll p_Nq
	      for(k=0;k<p_Nq;++k){
		id = e*p_Nfaces*p_Nfp + face*p_Nfp + k*p_Nq + j;

		FM = advectionVelocityM[id];
		FP = advectionVelocityP[id];
		
	 	idM = vmapM[id];
		idP = vmapP[id];
		
		qM = r_q[es][k];
		qP = (idM!=idP) ? q[idP]: -qM;
		
		r_Aq[es][k] += FP*qP + FM*qM;
	      }
	    }

#pragma unroll p_Nq 	    
	    for(int k=0;k<p_Nq;++k){    
	      
	      const dlong base = e*p_Np*p_Nfields + k*p_Nq*p_Nq + j*p_Nq + i;
	      
	      dfloat r_resq = resq[base];
	      r_resq = rka*r_resq + dt*r_Aq[es][k];
	      r_q[es][k] += rkb*r_resq;
	      
	      resq[base] = r_resq;
	      qnew[base] = r_q[es][k];
	    }
	  }
	}
      }
    }
  }
}




@kernel void advectionCombinedSkewHex3D(const dlong Nelements,
					const dfloat dt,
					const dfloat rka,
					const dfloat rkb,
					@restrict const dfloat * vgeo,
					@restrict const dfloat * D,
					@restrict const dfloat * advectionVelocityJW,
					@restrict const dlong  * vmapM,
					@restrict const dlong  * vmapP,
					@restrict const dfloat * advectionVelocityM,
					@restrict const dfloat * advectionVelocityP,
					@restrict dfloat * resq,
					@restrict const dfloat * q,
					@restrict dfloat * qnew){
  
  // each thread-block outputs p_NblockAdvVol elements
  for(dlong eo=0;eo<Nelements;eo+=p_NblockAdvVol;@outer(0)){
    
    // collocation differentiation
    @shared dfloat s_D[p_Nq][p_Nq];
    
    // use 2*Nq^2 shared and 2*Nq*Nq*Nq registers 
    // [ 3D shared would required 3*Nq*Nq*Nq shared ]
    
    @shared dfloat s_F[p_NblockAdvVol][p_Nq][p_Nq];
    @shared dfloat s_G[p_NblockAdvVol][p_Nq][p_Nq];
    @shared dfloat s_q[p_NblockAdvVol][p_Nq][p_Nq];

    @exclusive dfloat r_q[p_NblockAdvVol][p_Nq];
    @exclusive dfloat r_Aq[p_NblockAdvVol][p_Nq];
    @exclusive dfloat r_Hk[p_NblockAdvVol];
    @exclusive dfloat r_Fr[p_NblockAdvVol];
    @exclusive dfloat r_Fs[p_NblockAdvVol];
    @exclusive dfloat r_Ft[p_NblockAdvVol];

    for(int j=0;j<p_Nq;++j;@inner(1)){
      for(int i=0;i<p_Nq;++i;@inner(0)){
	
#pragma unroll p_NblockAdvVol
	for(int es=0;es<p_NblockAdvVol;++es){
	  
	  const dlong e = eo+es;
	  if(e<Nelements){

#pragma unroll p_Nq
	    for(int k=0;k<p_Nq;++k){
	      const dlong  qbase = e*p_Np*p_Nfields + k*p_Nq*p_Nq + j*p_Nq + i;
	      
	      r_q[es][k] = q[qbase+0*p_Np];
	    }
	  }

#pragma unroll p_Nq
	  for(int k=0;k<p_Nq;++k){
	    r_Aq[es][k] = 0;
	  }
	}

	s_D[j][i] = D[j*p_Nq+i];
      }
    }
    
#pragma unroll p_Nq
    for(int k=0;k<p_Nq;++k){
      
      @barrier("local");
      
      for(int j=0;j<p_Nq;++j;@inner(1)){
        for(int i=0;i<p_Nq;++i;@inner(0)){
	  
#pragma unroll p_NblockAdvVol
	  for(int es=0;es<p_NblockAdvVol;++es){
	    
	    const dlong e = eo+es;
	    if(e<Nelements){
	      // geometric factors
	      const dlong gbase = e*p_Np*p_dim + k*p_Nq*p_Nq + j*p_Nq + i;

	      // J*W*c_x, J*W*c_y, J*W*c_z
	      r_Fr[es] = advectionVelocityJW[gbase+p_Np*0];
	      r_Fs[es] = advectionVelocityJW[gbase+p_Np*1];
	      r_Ft[es] = advectionVelocityJW[gbase+p_Np*2];
	      
	      const dfloat qn = r_q[es][k];

	      s_q[es][j][i] = qn;
	      s_F[es][j][i] = r_Fr[es]*qn;
	      s_G[es][j][i] = r_Fs[es]*qn;
	      r_Hk[es]      = r_Ft[es]*qn;
	    }
	  }
	}
      }
      
      @barrier("local");
      
      for(int j=0;j<p_Nq;++j;@inner(1)){
        for(int i=0;i<p_Nq;++i;@inner(0)){    

#pragma unroll p_Nq          
          for(int n=0;n<p_Nq;++n){
	    // read D^t
            const dfloat Dni = s_D[n][i]; 
            const dfloat Dnj = s_D[n][j];
            const dfloat Dkn = s_D[k][n];
	    const dfloat Din = s_D[i][n]; 
	    const dfloat Djn = s_D[j][n];

	    // 6*p_NblockAdvVol flops for 8*3 + p_NblockAdvVol*2*8 bytes from shared

#pragma unroll p_NblockAdvVol
	    for(int es=0;es<p_NblockAdvVol;++es){
	      // MISSING FACTOR OF 0.5 (added below)
	      r_Aq[es][k] += Dni*s_F[es][j][n];
	      r_Aq[es][k] += Dnj*s_G[es][n][i];
	      // THIS FEELS WRONG
	      r_Aq[es][n] += Dkn*r_Hk[es]; // can move this up

	      r_Aq[es][k] -= r_Fr[es]*Din*s_q[es][j][n];
	      r_Aq[es][k] -= r_Fs[es]*Djn*s_q[es][n][i];
	      r_Aq[es][k] -= r_Ft[es]*Dkn*r_q[es][n];
	    }
	  }
	}
      }
    }

    // do faces
    for(int j=0;j<p_Nq;++j;@inner(1)){
      for(int i=0;i<p_Nq;++i;@inner(0)){    

	//#pragma unroll p_NblockAdvVol
	for(int es=0;es<p_NblockAdvVol;++es){
	  
	  const dlong e = eo+es;
	  if(e<Nelements){

	    dlong id, idM, idP;
	    dfloat FM, FP, qM, qP;
	    
	    int k=0;


#pragma unroll p_Nq
	    for(k=0;k<p_Nq;++k){
	      const dlong gid = e*p_Np*p_Nvgeo+ k*p_Nq*p_Nq + j*p_Nq +i;
	      const dfloat invJW = vgeo[gid + p_IJWID*p_Np];
	      r_Aq[es][k] *= -p_half*invJW; // divide by mass
	    }
	    
	    // do face 0
	    id = e*p_Nfaces*p_Nfp + 0*p_Nfp + j*p_Nq + i;
	    FM = advectionVelocityM[id]; 
	    FP = advectionVelocityP[id];
	    
	    idM = vmapM[id];
	    idP = vmapP[id];

	    qM = r_q[es][0];
	    qP = (idM!=idP) ? q[idP]: -qM;
	    
	    r_Aq[es][0] += FP*qP + FM*qM;

	    // do face 5
	    id = e*p_Nfaces*p_Nfp + 5*p_Nfp + j*p_Nq + i;
	    FM = advectionVelocityM[id];
	    FP = advectionVelocityP[id];
	    
	    idM = vmapM[id];
	    idP = vmapP[id];

	    qM = r_q[es][p_Nq-1];
	    qP = (idM!=idP) ? q[idP]: -qM;
	    
	    r_Aq[es][p_Nq-1] += FP*qP + FM*qM;
	    
	    // do face 1 && face 3
	    if(j==0 || j==p_Nq-1){
	      int face = (j==0) ? 1:3;
	      
	      for(k=0;k<p_Nq;++k){
		id = e*p_Nfaces*p_Nfp + face*p_Nfp + k*p_Nq + i;

		FM = advectionVelocityM[id];
		FP = advectionVelocityP[id];
		
		idM = vmapM[id];
		idP = vmapP[id];
		
		qM = r_q[es][k];
		qP = (idM!=idP) ? q[idP]: -qM;
		
		r_Aq[es][k] += FP*qP + FM*qM;
	      }
	      
	    }

	
	    // do face 2 && face 4	    
	    if(i==0 || i==p_Nq-1){
	      int face = (i==0) ? 4:2;
	      
	      for(k=0;k<p_Nq;++k){
		id = e*p_Nfaces*p_Nfp + face*p_Nfp + k*p_Nq + j;

		FM = advectionVelocityM[id];
		FP = advectionVelocityP[id];
		
	 	idM = vmapM[id];
		idP = vmapP[id];
		
		qM = r_q[es][k];
		qP = (idM!=idP) ? q[idP]: -qM;
		
		r_Aq[es][k] += FP*qP + FM*qM;
	      }
	    }
	    
#pragma unroll p_Nq 	    
	    for(int k=0;k<p_Nq;++k){    
	      const dlong base = e*p_Np*p_Nfields + k*p_Nq*p_Nq + j*p_Nq + i;
	      
	      dfloat r_resq = resq[base];
	      r_resq = rka*r_resq + dt*r_Aq[es][k];
	      r_q[es][k] += rkb*r_resq;
	      
	      resq[base] = r_resq;
	      qnew[base] = r_q[es][k];
	    }
	  }
	}
      }
    }
  }
}


// weak form, with cubature
@kernel void advectionCubatureVolumeHex3D_v0(const dlong Nelements,
					  @restrict const dfloat * vgeo,
					  @restrict const dfloat * cubvgeo,
					  @restrict const dfloat * cubD,
					  @restrict const dfloat * cubInterpT,
					  @restrict const dfloat * cubProjectT,
					  @restrict const dfloat * cubAdvectionVelocityJW,
					  @restrict const dfloat * q,
					  @restrict dfloat * rhsq){
  
  //----------------------------@kernel 1 interpolate velocity fields to quadrature points
  for(dlong e=0; e<Nelements; ++e; @outer(0)) {

    @shared dfloat s_cubD[p_cubNq][p_cubNq];
    @shared dfloat s_cubInterpT[p_Nq][p_cubNq];
    @shared dfloat s_cubProjectT[p_cubNq][p_Nq];

    @shared dfloat s_F1[p_cubNq][p_cubNq];
    @shared dfloat s_F2[p_cubNq][p_cubNq];
    @shared dfloat s_F3[p_cubNq][p_cubNq];

    @shared dfloat s_q[p_cubNq][p_cubNq];
    @shared dfloat s_rhsq[p_cubNq][p_cubNq];
    
    @exclusive dfloat r_cq[p_cubNq];
    @exclusive dfloat r_rhsq[p_cubNq];

    for(int j=0;j<p_cubNq;++j;@inner(1)){
      for(int i=0;i<p_cubNq;++i;@inner(0)){

        const int id = i+j*p_cubNq;
        if (id<p_Nq*p_cubNq) {
          s_cubInterpT[0][id] = cubInterpT[id];
          s_cubProjectT[0][id] = cubProjectT[id];
        }
        s_cubD[0][id] = cubD[id];
	
#pragma unroll p_cubNq
	for(int k=0;k<p_cubNq;++k){         
	  r_cq[k] = 0.;
	  r_rhsq[k] = 0.;
	}
      }
    }

    @barrier("local");

    //read in and interpolate in k
    for(int j=0;j<p_cubNq;++j;@inner(1)){
      for(int i=0;i<p_cubNq;++i;@inner(0)){
        if (i<p_Nq && j<p_Nq) {
#pragma unroll p_Nq
          for(int k=0;k<p_Nq;++k){
            const dlong id = e*p_Np + k*p_Nq*p_Nq + j*p_Nq + i;
            const dfloat qc = q[id];

#pragma unroll p_cubNq
            for(int n=0;n<p_cubNq;++n){
              const dfloat Ik = s_cubInterpT[k][n];
              r_cq[n] += Ik*qc;
            }
          }
        }
      }
    }
    
    @barrier("local");

#pragma unroll p_cubNq
    for(int k=0;k<p_cubNq;++k){ 
      //load slice to @shared
      for(int j=0;j<p_cubNq;++j;@inner(1)){ 
        for(int i=0;i<p_cubNq;++i;@inner(0)){
          if (i<p_Nq && j<p_Nq) {
            s_q[j][i] = r_cq[k];
          }
        }
      }

      @barrier("local");

      for(int j=0;j<p_cubNq;++j;@inner(1)){ 
        for(int i=0;i<p_cubNq;++i;@inner(0)){
          if(j<p_Nq){
            r_cq[k] = 0; 

#pragma unroll p_Nq
            for (int n=0;n<p_Nq;n++) {
              const dfloat Ii = s_cubInterpT[n][i];
              r_cq[k] += Ii*s_q[j][n];
            }
          }
        }
      }

      @barrier("local");

      for(int j=0;j<p_cubNq;++j;@inner(1)){ 
        for(int i=0;i<p_cubNq;++i;@inner(0)){
          if(j<p_Nq){
            s_q[j][i] = r_cq[k];
          }
        }
      }

      @barrier("local");

      for(int j=0;j<p_cubNq;++j;@inner(1)){ 
        for(int i=0;i<p_cubNq;++i;@inner(0)){
          r_cq[k] = 0;

          #pragma unroll p_Nq
          for (int n=0;n<p_Nq;n++) {
            const dfloat Ij = s_cubInterpT[n][j];
            r_cq[k] += Ij*s_q[n][i];
          }
        }
      }

      @barrier("local");      
    }

#pragma unroll p_cubNq
    for(int k=0;k<p_cubNq;++k){ 

      //fetch slice
      for(int j=0;j<p_cubNq;++j;@inner(1)){ 
        for(int i=0;i<p_cubNq;++i;@inner(0)){
          const dlong gid = e*p_cubNp*p_dim+ k*p_cubNq*p_cubNq + j*p_cubNq +i;
          const dfloat cr = cubAdvectionVelocityJW[gid + 0*p_cubNp];
	  const dfloat cs = cubAdvectionVelocityJW[gid + 1*p_cubNp];
	  const dfloat ct = cubAdvectionVelocityJW[gid + 2*p_cubNp];
          const dfloat JW   = cubvgeo[gid + p_JWID*p_cubNp];

          const dfloat qn = r_cq[k];
          const dfloat cqrn = (cr*qn);
          const dfloat cqsn = (cs*qn);
          const dfloat cqtn = (ct*qn);

          s_F1[j][i] = cqrn;
          s_F2[j][i] = cqsn;
          s_F3[j][i] = cqtn;
        }
      }

      @barrier("local");

      for(int j=0;j<p_cubNq;++j;@inner(1)){ 
        for(int i=0;i<p_cubNq;++i;@inner(0)){
          #pragma unroll p_cubNq
            for (int n=0;n<p_cubNq;n++) {
              const dfloat Dr = s_cubD[n][i];
              const dfloat Ds = s_cubD[n][j];
              const dfloat Dt = s_cubD[k][n];
              r_rhsq[k] += Dr*s_F1[j][n];
              r_rhsq[k] += Ds*s_F2[n][i];
              r_rhsq[n] += Dt*s_F3[j][i];
            }
	}
      }

      @barrier("local");
    } //k loop

#pragma unroll p_cubNq
    for(int k=0;k<p_cubNq;++k){ 
      //load slice to @shared
      for(int j=0;j<p_cubNq;++j;@inner(1)){ 
        for(int i=0;i<p_cubNq;++i;@inner(0)){
          s_rhsq[j][i] = r_rhsq[k];
        }
      }
      
      @barrier("local");
      
      for(int j=0;j<p_cubNq;++j;@inner(1)){ 
        for(int i=0;i<p_cubNq;++i;@inner(0)){
          if(j<p_Nq){
            r_rhsq[k] = 0; 
	    
#pragma unroll p_cubNq
            for (int n=0;n<p_cubNq;n++) {
              const dfloat Pj = s_cubProjectT[n][j];
              r_rhsq[k] += Pj*s_rhsq[n][i];
            }
          }
        }
      }

      @barrier("local");

      for(int j=0;j<p_cubNq;++j;@inner(1)){ 
        for(int i=0;i<p_cubNq;++i;@inner(0)){
          if(j<p_Nq){
            s_rhsq[j][i] = r_rhsq[k];
          }
        }
      }

      @barrier("local");

      for(int j=0;j<p_cubNq;++j;@inner(1)){ 
        for(int i=0;i<p_cubNq;++i;@inner(0)){
          if ((i<p_Nq) && (j<p_Nq)) {
            r_rhsq[k] = 0; 

            #pragma unroll p_cubNq
            for (int n=0;n<p_cubNq;n++) {
              const dfloat Pi = s_cubProjectT[n][i];
              r_rhsq[k] += Pi*s_rhsq[j][n];
            }
          }
        }
      }
      
      @barrier("local");
    }

    //project in k and write out
    for(int j=0;j<p_cubNq;++j;@inner(1)){
      for(int i=0;i<p_cubNq;++i;@inner(0)){
        if ((i<p_Nq) && (j<p_Nq)) {
#pragma unroll p_Nq
          for(int k=0;k<p_Nq;++k){    
            dfloat rhsqk=0.;

            #pragma unroll p_cubNq
            for(int n=0;n<p_cubNq;++n){
              const dfloat Pk = s_cubProjectT[n][k];
              rhsqk += Pk*r_rhsq[n];
            }

            const dlong gid = e*p_Np*p_Nvgeo+ k*p_Nq*p_Nq + j*p_Nq +i;
            const dfloat invJW = vgeo[gid + p_IJWID*p_Np];

            const dlong id = e*p_Np + k*p_Nq*p_Nq + j*p_Nq + i;
            rhsq[id] = -invJW*rhsqk;
          }
        }
      }
    }
  }
}


// weak form, with cubature and blocking
@kernel void advectionCubatureVolumeHex3D_v1(const dlong Nelements,
					  @restrict const dfloat * vgeo,
					  @restrict const dfloat * cubvgeo,
					  @restrict const dfloat * cubD,
					  @restrict const dfloat * cubInterpT,
					  @restrict const dfloat * cubProjectT,
					  @restrict const dfloat * cubAdvectionVelocityJW,
					  @restrict const dfloat * q,
					  @restrict dfloat * rhsq){
  
  //----------------------------@kernel 1 interpolate velocity fields to quadrature points
  for(dlong eo=0; eo<Nelements; eo+=p_NblockAdvVol; @outer(0)) {

    @shared dfloat s_cubD[p_cubNq][p_cubNq];
    @shared dfloat s_cubInterpT[p_Nq][p_cubNq];
    @shared dfloat s_cubProjectT[p_cubNq][p_Nq];

    @shared dfloat s_F1[p_NblockAdvVol][p_cubNq][p_cubNq];
    @shared dfloat s_F2[p_NblockAdvVol][p_cubNq][p_cubNq];
    @shared dfloat s_F3[p_NblockAdvVol][p_cubNq][p_cubNq];

    @shared dfloat s_q[p_NblockAdvVol][p_cubNq][p_cubNq];
    @shared dfloat s_rhsq[p_NblockAdvVol][p_cubNq][p_cubNq];
    
    @exclusive dfloat r_cq[p_NblockAdvVol][p_cubNq];
    @exclusive dfloat r_rhsq[p_NblockAdvVol][p_cubNq];

    for(int j=0;j<p_cubNq;++j;@inner(1)){
      for(int i=0;i<p_cubNq;++i;@inner(0)){

        const int id = i+j*p_cubNq;
        if (id<p_Nq*p_cubNq) {
          s_cubInterpT[0][id] = cubInterpT[id];
          s_cubProjectT[0][id] = cubProjectT[id];
        }
        s_cubD[0][id] = cubD[id];
	
#pragma unroll p_NblockAdvVol
	for(int es=0;es<p_NblockAdvVol;++es){
	  
#pragma unroll p_cubNq
	  for(int k=0;k<p_cubNq;++k){         
	    r_cq[es][k] = 0.;
	    r_rhsq[es][k] = 0.;
	  }
	}
      }
    }
    
    @barrier("local");
    
    //read in and interpolate in k
    for(int j=0;j<p_cubNq;++j;@inner(1)){
      for(int i=0;i<p_cubNq;++i;@inner(0)){
        if (i<p_Nq && j<p_Nq) {
	  dfloat r_qc[p_NblockAdvVol];
	  
	  // do this outer so that fetches are blocked
#pragma unroll p_Nq
	  for(int k=0;k<p_Nq;++k){
	    
#pragma unroll p_NblockAdvVol
	    for(int es=0;es<p_NblockAdvVol;++es){
	      const int e = es+eo;
	      const dlong id = e*p_Np + k*p_Nq*p_Nq + j*p_Nq + i;
	      r_qc[es] = (e<Nelements) ? q[id]:0;
	    }
	    
#pragma unroll p_cubNq
	    for(int n=0;n<p_cubNq;++n){
	      const dfloat Ik = s_cubInterpT[k][n];
#pragma unroll p_NblockAdvVol
	      for(int es=0;es<p_NblockAdvVol;++es){
		r_cq[es][n] += Ik*r_qc[es];
	      }
	    }
	  }
	}
      }
    }
    
    @barrier("local");
    
#pragma unroll p_cubNq
    for(int k=0;k<p_cubNq;++k){ 
      //load slice to @shared
      for(int j=0;j<p_cubNq;++j;@inner(1)){ 
        for(int i=0;i<p_cubNq;++i;@inner(0)){
          if (i<p_Nq && j<p_Nq) {
#pragma unroll p_NblockAdvVol
	    for(int es=0;es<p_NblockAdvVol;++es){
	      s_q[es][j][i] = r_cq[es][k];
	    }
          }
        }
      }
      
      @barrier("local");
      
      for(int j=0;j<p_cubNq;++j;@inner(1)){ 
        for(int i=0;i<p_cubNq;++i;@inner(0)){
          if(j<p_Nq){
	    for(int es=0;es<p_NblockAdvVol;++es){
	      r_cq[es][k] = 0; 
	    }
	    
#pragma unroll p_Nq
            for (int n=0;n<p_Nq;n++) {
              const dfloat Ii = s_cubInterpT[n][i];
#pragma unroll p_NblockAdvVol
	      for(int es=0;es<p_NblockAdvVol;++es){
		r_cq[es][k] += Ii*s_q[es][j][n];
	      }
            }
          }
        }
      }
      
      @barrier("local");
      
      for(int j=0;j<p_cubNq;++j;@inner(1)){ 
        for(int i=0;i<p_cubNq;++i;@inner(0)){
          if(j<p_Nq){
#pragma unroll p_NblockAdvVol
	    for(int es=0;es<p_NblockAdvVol;++es){
	      s_q[es][j][i] = r_cq[es][k];
	    }
          }
        }
      }
      
      @barrier("local");
      
      for(int j=0;j<p_cubNq;++j;@inner(1)){ 
        for(int i=0;i<p_cubNq;++i;@inner(0)){
#pragma unroll p_NblockAdvVol
	  for(int es=0;es<p_NblockAdvVol;++es){
	    r_cq[es][k] = 0;
	  }
	  
#pragma unroll p_Nq
          for (int n=0;n<p_Nq;n++) {
            const dfloat Ij = s_cubInterpT[n][j];
#pragma unroll p_NblockAdvVol
	    for(int es=0;es<p_NblockAdvVol;++es){
	      r_cq[es][k] += Ij*s_q[es][n][i];
	    }
          }
        }
      }
    
      @barrier("local");      
    }
    
#pragma unroll p_cubNq
    for(int k=0;k<p_cubNq;++k){ 
      
      //fetch slice
      for(int j=0;j<p_cubNq;++j;@inner(1)){ 
	for(int i=0;i<p_cubNq;++i;@inner(0)){
	  
#pragma unroll p_NblockAdvVol
	  for(int es=0;es<p_NblockAdvVol;++es){
	    
	    const int e = es+eo;
	    if(e<Nelements){
	      
	      const dlong gid = e*p_cubNp*p_dim+ k*p_cubNq*p_cubNq + j*p_cubNq +i;
	      const dfloat cr = cubAdvectionVelocityJW[gid + 0*p_cubNp];
	      const dfloat cs = cubAdvectionVelocityJW[gid + 1*p_cubNp];
	      const dfloat ct = cubAdvectionVelocityJW[gid + 2*p_cubNp];
	      const dfloat JW   = cubvgeo[gid + p_JWID*p_cubNp];
	      
	      const dfloat qn = r_cq[es][k];
	      const dfloat cqrn = (cr*qn);
	      const dfloat cqsn = (cs*qn);
	      const dfloat cqtn = (ct*qn);
	      
	      s_F1[es][j][i] = cqrn;
	      s_F2[es][j][i] = cqsn;
	      s_F3[es][j][i] = cqtn;
	    }
	  }
	}
      }
      
      @barrier("local");
      
      for(int j=0;j<p_cubNq;++j;@inner(1)){ 
        for(int i=0;i<p_cubNq;++i;@inner(0)){
#pragma unroll p_cubNq
	  for (int n=0;n<p_cubNq;n++) {
	    const dfloat Dr = s_cubD[n][i];
	    const dfloat Ds = s_cubD[n][j];
	    const dfloat Dt = s_cubD[k][n];

#pragma unroll p_NblockAdvVol
	    for(int es=0;es<p_NblockAdvVol;++es){
	      
	      r_rhsq[es][k] += Dr*s_F1[es][j][n];
              r_rhsq[es][k] += Ds*s_F2[es][n][i];
              r_rhsq[es][n] += Dt*s_F3[es][j][i];
            }
	  }
	}
      }
      @barrier("local");
    }//k loop
      
#pragma unroll p_cubNq
    for(int k=0;k<p_cubNq;++k){ 
      //load slice to @shared
      for(int j=0;j<p_cubNq;++j;@inner(1)){ 
        for(int i=0;i<p_cubNq;++i;@inner(0)){
	  
#pragma unroll p_NblockAdvVol
	  for(int es=0;es<p_NblockAdvVol;++es){
	    s_rhsq[es][j][i] = r_rhsq[es][k];
	  }
        }
      }
      
      @barrier("local");
      
      for(int j=0;j<p_cubNq;++j;@inner(1)){ 
        for(int i=0;i<p_cubNq;++i;@inner(0)){
          if(j<p_Nq){
#pragma unroll p_NblockAdvVol
	    for(int es=0;es<p_NblockAdvVol;++es){
	      r_rhsq[es][k] = 0;
	    }
	    
#pragma unroll p_cubNq
            for (int n=0;n<p_cubNq;n++) {
              const dfloat Pj = s_cubProjectT[n][j];

#pragma unroll p_NblockAdvVol
	      for(int es=0;es<p_NblockAdvVol;++es){
		r_rhsq[es][k] += Pj*s_rhsq[es][n][i];
	      }
            }
          }
        }
      }
    
      @barrier("local");
      
      for(int j=0;j<p_cubNq;++j;@inner(1)){ 
        for(int i=0;i<p_cubNq;++i;@inner(0)){
          if(j<p_Nq){
#pragma unroll p_NblockAdvVol
	    for(int es=0;es<p_NblockAdvVol;++es){
	      s_rhsq[es][j][i] = r_rhsq[es][k];
	    }
          }
        }
      }
      
      @barrier("local");
      
      for(int j=0;j<p_cubNq;++j;@inner(1)){ 
        for(int i=0;i<p_cubNq;++i;@inner(0)){
          if ((i<p_Nq) && (j<p_Nq)) {
#pragma unroll p_NblockAdvVol
	    for(int es=0;es<p_NblockAdvVol;++es){
	      r_rhsq[es][k] = 0;
	    }
	    
#pragma unroll p_cubNq
            for (int n=0;n<p_cubNq;n++) {
              const dfloat Pi = s_cubProjectT[n][i];

#pragma unroll p_NblockAdvVol
	      for(int es=0;es<p_NblockAdvVol;++es){
		r_rhsq[es][k] += Pi*s_rhsq[es][j][n];
	      }
            }
          }
        }
      }
      
      @barrier("local");
    }// k loop
    
    //project in k and write out
    for(int j=0;j<p_cubNq;++j;@inner(1)){
      for(int i=0;i<p_cubNq;++i;@inner(0)){
        if ((i<p_Nq) && (j<p_Nq)) {

#pragma unroll p_Nq
	  for(int k=0;k<p_Nq;++k){
	    
	    dfloat r_rhsqout[p_NblockAdvVol];
#pragma unroll p_NblockAdvVol
	    for(int es=0;es<p_NblockAdvVol;++es){
	      r_rhsqout[es] = 0;
	    }
	    
#pragma unroll p_cubNq
	    for(int n=0;n<p_cubNq;++n){
	      const dfloat Pk = s_cubProjectT[n][k];

#pragma unroll p_NblockAdvVol
	      for(int es=0;es<p_NblockAdvVol;++es){
		r_rhsqout[es] += Pk*r_rhsq[es][n];
	      }
	    }
	    
#pragma unroll p_NblockAdvVol
	    for(int es=0;es<p_NblockAdvVol;++es){
	      
	      const int e = es+eo;
	      if(e<Nelements){
		const dlong gid = e*p_Np*p_Nvgeo+ k*p_Nq*p_Nq + j*p_Nq +i;
		const dfloat invJW = vgeo[gid + p_IJWID*p_Np];
		
		const dlong id = e*p_Np + k*p_Nq*p_Nq + j*p_Nq + i;
		rhsq[id] = -invJW*r_rhsqout[es];
	      }
	    }
	  }
	}
      }
    }
  }
}


// 3 way reuse of D,P,I

// weak form, with cubature and blocking
@kernel void advectionCubatureVolumeHex3D(const dlong Nelements,
					  @restrict const dfloat * vgeo,
					  @restrict const dfloat * cubvgeo,
					  @restrict const dfloat * cubD,
					  @restrict const dfloat * cubInterpT,
					  @restrict const dfloat * cubProjectT,
					  @restrict const dfloat * cubAdvectionVelocityJW,
					  @restrict const dfloat * q,
					  @restrict dfloat * rhsq){

  for(dlong e=0; e<Nelements; ++e; @outer(0)) {

#if p_Nq==8 
#define p_pad 1
#else
#define p_pad 0
#endif

#if p_cubNq==11
#define p_padI 1
#else
#define p_padI 0
#endif

    
    @shared    dfloat s_q[p_Nq][p_Nq][p_Nq+p_pad];
    @exclusive dfloat r_Iqi[p_cubNq], r_Iqj[p_cubNq], r_Iqk[p_cubNq];
    @exclusive dfloat r_PDIqi[p_Nq], r_PDIqj[p_Nq], r_PDIqk[p_Nq];

    @shared    dfloat s_P[p_Nq][p_cubNq], s_D[p_cubNq][p_cubNq], s_I[p_cubNq][p_Nq+p_padI];
    
    for(int j=0;j<p_Nq;++j;@inner(1)){
      for(int i=0;i<p_Nq;++i;@inner(0)){

#pragma unroll p_Nq
	for(int k=0;k<p_Nq;++k){
	  dlong base = e*p_Np + k*p_Nq*p_Nq + j*p_Nq + i;
	  s_q[k][j][i] = q[base];
	}

	int n = j*p_Nq + i;
	while(n<p_cubNq*p_Nq){
	  s_P[0][n] = cubProjectT[n];
	  s_I[0][n] = cubInterpT[n];
	  n+=p_Nq*p_Nq;
	}

	n = j*p_Nq + i;
	while(n<p_cubNq*p_cubNq){
	  s_D[0][n] = cubD[n];
	  n+=p_Nq*p_Nq;
	}
	
      }
    }
    
    @barrier("local");
    
    for(int b=0;b<p_Nq;++b;@inner(1)){
      for(int a=0;a<p_Nq;++a;@inner(0)){

#pragma unroll p_cubNq
	for(int c=0;c<p_cubNq;++c){
	  r_Iqi[c] = 0;
	  r_Iqj[c] = 0;
	  r_Iqk[c] = 0;
	}

#pragma unroll p_Nq
	for(int c=0;c<p_Nq;++c){
	  dfloat r_qi = s_q[b][a][c];
	  dfloat r_qj = s_q[b][c][a];
	  dfloat r_qk = s_q[c][b][a];

#pragma unroll p_cubNq
	  for(int n=0;n<p_cubNq;++n){
	    const dfloat Inc = s_I[n][c];// 6 flops for 8 bytes (note transpose)
	    r_Iqi[n] += Inc*r_qi;
	    r_Iqj[n] += Inc*r_qj;
	    r_Iqk[n] += Inc*r_qk;
	  }
	}

#pragma unroll p_cubNq
	for(int n=0;n<p_cubNq;++n){
	  const dlong gid = 3*e*p_Nq*p_Nq*p_cubNq + b*p_Nq*p_cubNq+ a*p_cubNq + n;
	  const dfloat cr = cubAdvectionVelocityJW[gid + 0*p_Nq*p_Nq*p_cubNq];
	  const dfloat cs = cubAdvectionVelocityJW[gid + 1*p_Nq*p_Nq*p_cubNq];
	  const dfloat ct = cubAdvectionVelocityJW[gid + 2*p_Nq*p_Nq*p_cubNq];

	  r_Iqi[n] *= cr;
	  r_Iqj[n] *= cs;
	  r_Iqk[n] *= ct;
	}

#pragma unroll p_Nq
	for(int m=0;m<p_Nq;++m){
	  r_PDIqi[m] = 0;
	  r_PDIqj[m] = 0;
	  r_PDIqk[m] = 0;
	}

#pragma unroll p_cubNq
	for(int n=0;n<p_cubNq;++n){
	  
	  dfloat r_DIqi = 0, r_DIqj = 0, r_DIqk = 0;

#pragma unroll p_cubNq
	  for(int m=0;m<p_cubNq;++m){
	    // all threads read the same
	    const dfloat Dnm = s_D[n][m]; // 6 flops for 8 bytes
	    r_DIqi += Dnm*r_Iqi[m];
	    r_DIqj += Dnm*r_Iqj[m];
	    r_DIqk += Dnm*r_Iqk[m];
	  }
	  
	  // fix this later - will multiply by 1/J
	  const dlong base = 3*e*p_Nq*p_Nq*p_cubNq + b*p_Nq*p_cubNq+ a*p_cubNq + n;
	  
	  r_DIqi *= cubvgeo[base+0*p_Nq*p_Nq*p_cubNq];
	  r_DIqj *= cubvgeo[base+1*p_Nq*p_Nq*p_cubNq];
	  r_DIqk *= cubvgeo[base+2*p_Nq*p_Nq*p_cubNq];

#pragma unroll p_Nq
	  for(int m=0;m<p_Nq;++m){
	    // all threads reas the same
	    const dfloat Pmn = s_P[m][n];  // 6 flops for 8 bytes (12*6/8 TFLOPS/s)
	    r_PDIqi[m] += Pmn*r_DIqi;
	    r_PDIqj[m] += Pmn*r_DIqj;
	    r_PDIqk[m] += Pmn*r_DIqk;
	  }
	}
	
      } // end a
    } // end b
    
    @barrier("local");
    
    for(int b=0;b<p_Nq;++b;@inner(1)){
      for(int a=0;a<p_Nq;++a;@inner(0)){
#pragma unroll p_Nq
	for(int c=0;c<p_Nq;++c){
	  s_q[b][a][c] = r_PDIqi[c];
	}
      }
    }
    
    @barrier("local");

    for(int b=0;b<p_Nq;++b;@inner(1)){
      for(int a=0;a<p_Nq;++a;@inner(0)){
#pragma unroll p_Nq
	for(int c=0;c<p_Nq;++c){
	  s_q[b][c][a] += r_PDIqj[c];
	}
      }
    }
    
    @barrier("local");

    for(int b=0;b<p_Nq;++b;@inner(1)){
      for(int a=0;a<p_Nq;++a;@inner(0)){
#pragma unroll p_Nq
	for(int c=0;c<p_Nq;++c){
	  
	  const dfloat qbca = s_q[c][b][a] + r_PDIqk[c];
	  const dlong id = e*p_Np + c*p_Nq*p_Nq + b*p_Nq + a;
	  
	  rhsq[id] = qbca;
	}
      }
    }
  }
}


// skew form, with cubature and blocking
@kernel void advectionCubatureSkewVolumeHex3D_v0(const dlong Nelements,
					      @restrict const dfloat * vgeo,
					      @restrict const dfloat * cubvgeo,
					      @restrict const dfloat * cubD,
					      @restrict const dfloat * cubInterpT,
					      @restrict const dfloat * cubProjectT,
					      @restrict const dfloat * cubAdvectionVelocityJW,
					      @restrict const dfloat * q,
					      @restrict dfloat * rhsq){
  
  //----------------------------@kernel 1 interpolate velocity fields to quadrature points
  for(dlong eo=0; eo<Nelements; eo+=p_NblockAdvVol; @outer(0)) {
    
    @shared dfloat s_cubD[p_cubNq][p_cubNq];
    @shared dfloat s_cubInterpT[p_Nq][p_cubNq];
    @shared dfloat s_cubProjectT[p_cubNq][p_Nq];

    @shared dfloat s_F1[p_NblockAdvVol][p_cubNq][p_cubNq];
    @shared dfloat s_F2[p_NblockAdvVol][p_cubNq][p_cubNq];
    @shared dfloat s_F3[p_NblockAdvVol][p_cubNq][p_cubNq];

    @shared dfloat s_q[p_NblockAdvVol][p_cubNq][p_cubNq];
    @shared dfloat s_rhsq[p_NblockAdvVol][p_cubNq][p_cubNq];
    
    @exclusive dfloat r_cq[p_NblockAdvVol][p_cubNq];
    @exclusive dfloat r_rhsq[p_NblockAdvVol][p_cubNq];

    @exclusive dfloat r_Fr[p_NblockAdvVol];
    @exclusive dfloat r_Fs[p_NblockAdvVol];
    @exclusive dfloat r_Ft[p_NblockAdvVol];
    
    for(int j=0;j<p_cubNq;++j;@inner(1)){
      for(int i=0;i<p_cubNq;++i;@inner(0)){

        const int id = i+j*p_cubNq;
        if (id<p_Nq*p_cubNq) {
          s_cubInterpT[0][id] = cubInterpT[id];
          s_cubProjectT[0][id] = cubProjectT[id];
        }
        s_cubD[0][id] = cubD[id];
	
#pragma unroll p_NblockAdvVol
	for(int es=0;es<p_NblockAdvVol;++es){
	  
#pragma unroll p_cubNq
	  for(int k=0;k<p_cubNq;++k){         
	    r_cq[es][k] = 0.;
	    r_rhsq[es][k] = 0.;
	  }
	}
      }
    }
    
    @barrier("local");
    
    //read in and interpolate in k
    for(int j=0;j<p_cubNq;++j;@inner(1)){
      for(int i=0;i<p_cubNq;++i;@inner(0)){
        if (i<p_Nq && j<p_Nq) {
	  dfloat r_qin[p_NblockAdvVol];
	  
	  // do this outer so that fetches are blocked
#pragma unroll p_Nq
	  for(int k=0;k<p_Nq;++k){
	    
#pragma unroll p_NblockAdvVol
	    for(int es=0;es<p_NblockAdvVol;++es){
	      const int e = es+eo;
	      const dlong id = e*p_Np + k*p_Nq*p_Nq + j*p_Nq + i;
	      r_qin[es] = (e<Nelements) ? q[id]:0;
	    }
	    
#pragma unroll p_cubNq
	    for(int n=0;n<p_cubNq;++n){
	      const dfloat Ik = s_cubInterpT[k][n];
#pragma unroll p_NblockAdvVol
	      for(int es=0;es<p_NblockAdvVol;++es){
		r_cq[es][n] += Ik*r_qin[es];
	      }
	    }
	  }
	}
      }
    }
    
    @barrier("local");
    
#pragma unroll p_cubNq
    for(int k=0;k<p_cubNq;++k){ 
      //load slice to @shared
      for(int j=0;j<p_cubNq;++j;@inner(1)){ 
        for(int i=0;i<p_cubNq;++i;@inner(0)){
          if (i<p_Nq && j<p_Nq) {
#pragma unroll p_NblockAdvVol
	    for(int es=0;es<p_NblockAdvVol;++es){
	      s_q[es][j][i] = r_cq[es][k];
	    }
          }
        }
      }
      
      @barrier("local");
      
      for(int j=0;j<p_cubNq;++j;@inner(1)){ 
        for(int i=0;i<p_cubNq;++i;@inner(0)){
          if(j<p_Nq){
	    for(int es=0;es<p_NblockAdvVol;++es){
	      r_cq[es][k] = 0; 
	    }
	    
#pragma unroll p_Nq
            for (int n=0;n<p_Nq;n++) {
              const dfloat Ii = s_cubInterpT[n][i];
#pragma unroll p_NblockAdvVol
	      for(int es=0;es<p_NblockAdvVol;++es){
		r_cq[es][k] += Ii*s_q[es][j][n];
	      }
            }
          }
        }
      }
      
      @barrier("local");
      
      for(int j=0;j<p_cubNq;++j;@inner(1)){ 
        for(int i=0;i<p_cubNq;++i;@inner(0)){
          if(j<p_Nq){
#pragma unroll p_NblockAdvVol
	    for(int es=0;es<p_NblockAdvVol;++es){
	      s_q[es][j][i] = r_cq[es][k];
	    }
          }
        }
      }
      
      @barrier("local");
      
      for(int j=0;j<p_cubNq;++j;@inner(1)){ 
        for(int i=0;i<p_cubNq;++i;@inner(0)){
#pragma unroll p_NblockAdvVol
	  for(int es=0;es<p_NblockAdvVol;++es){
	    r_cq[es][k] = 0;
	  }
	  
#pragma unroll p_Nq
          for (int n=0;n<p_Nq;n++) {
            const dfloat Ij = s_cubInterpT[n][j];
#pragma unroll p_NblockAdvVol
	    for(int es=0;es<p_NblockAdvVol;++es){
	      r_cq[es][k] += Ij*s_q[es][n][i];
	    }
          }
        }
      }
    
      @barrier("local");      
    }
    
#pragma unroll p_cubNq
    for(int k=0;k<p_cubNq;++k){ 
      
      //fetch slice
      for(int j=0;j<p_cubNq;++j;@inner(1)){ 
	for(int i=0;i<p_cubNq;++i;@inner(0)){
	  
#pragma unroll p_NblockAdvVol
	  for(int es=0;es<p_NblockAdvVol;++es){
	    
	    const int e = es+eo;
	    if(e<Nelements){
	      
	      const dlong gid = e*p_cubNp*p_dim+ k*p_cubNq*p_cubNq + j*p_cubNq +i;
	      r_Fr[es] = cubAdvectionVelocityJW[gid + 0*p_cubNp];
	      r_Fs[es] = cubAdvectionVelocityJW[gid + 1*p_cubNp];
	      r_Ft[es] = cubAdvectionVelocityJW[gid + 2*p_cubNp];
	      const dfloat JW   = cubvgeo[gid + p_JWID*p_cubNp];
	      
	      const dfloat qn = r_cq[es][k];
	      const dfloat cqrn = (r_Fr[es]*qn);
	      const dfloat cqsn = (r_Fs[es]*qn);
	      const dfloat cqtn = (r_Ft[es]*qn);
	      
	      s_F1[es][j][i] = cqrn;
	      s_F2[es][j][i] = cqsn;
	      s_F3[es][j][i] = cqtn;
	      s_q[es][j][i] = qn;
	    }
	  }
	}
      }
      
      @barrier("local");
      
      for(int j=0;j<p_cubNq;++j;@inner(1)){ 
        for(int i=0;i<p_cubNq;++i;@inner(0)){
#pragma unroll p_cubNq
	  for (int n=0;n<p_cubNq;n++) {
	    const dfloat Dni = s_cubD[n][i];
	    const dfloat Dnj = s_cubD[n][j];
	    const dfloat Dkn = s_cubD[k][n];
	    const dfloat Din = s_cubD[i][n];
	    const dfloat Djn = s_cubD[j][n];

#pragma unroll p_NblockAdvVol
	    for(int es=0;es<p_NblockAdvVol;++es){
	      
	      r_rhsq[es][k] += Dni*s_F1[es][j][n];
              r_rhsq[es][k] += Dnj*s_F2[es][n][i];
              r_rhsq[es][n] += Dkn*s_F3[es][j][i];

	      r_rhsq[es][k] -= r_Fr[es]*Din*s_q[es][j][n];
	      r_rhsq[es][k] -= r_Fs[es]*Djn*s_q[es][n][i];
	      r_rhsq[es][k] -= r_Ft[es]*Dkn*r_cq[es][n];
            }
	  }
	}
      }
      @barrier("local");
    }//k loop
    
#pragma unroll p_cubNq
    for(int k=0;k<p_cubNq;++k){ 
      //load slice to @shared
      for(int j=0;j<p_cubNq;++j;@inner(1)){ 
        for(int i=0;i<p_cubNq;++i;@inner(0)){
	  
#pragma unroll p_NblockAdvVol
	  for(int es=0;es<p_NblockAdvVol;++es){
	    s_rhsq[es][j][i] = r_rhsq[es][k];
	  }
        }
      }
      
      @barrier("local");
      
      for(int j=0;j<p_cubNq;++j;@inner(1)){ 
        for(int i=0;i<p_cubNq;++i;@inner(0)){
          if(j<p_Nq){
#pragma unroll p_NblockAdvVol
	    for(int es=0;es<p_NblockAdvVol;++es){
	      r_rhsq[es][k] = 0;
	    }
	    
#pragma unroll p_cubNq
            for (int n=0;n<p_cubNq;n++) {
              const dfloat Pj = s_cubProjectT[n][j];

#pragma unroll p_NblockAdvVol
	      for(int es=0;es<p_NblockAdvVol;++es){
		r_rhsq[es][k] += Pj*s_rhsq[es][n][i];
	      }
            }
          }
        }
      }
    
      @barrier("local");
      
      for(int j=0;j<p_cubNq;++j;@inner(1)){ 
        for(int i=0;i<p_cubNq;++i;@inner(0)){
          if(j<p_Nq){
#pragma unroll p_NblockAdvVol
	    for(int es=0;es<p_NblockAdvVol;++es){
	      s_rhsq[es][j][i] = r_rhsq[es][k];
	    }
          }
        }
      }
      
      @barrier("local");
      
      for(int j=0;j<p_cubNq;++j;@inner(1)){ 
        for(int i=0;i<p_cubNq;++i;@inner(0)){
          if ((i<p_Nq) && (j<p_Nq)) {
#pragma unroll p_NblockAdvVol
	    for(int es=0;es<p_NblockAdvVol;++es){
	      r_rhsq[es][k] = 0;
	    }
	    
#pragma unroll p_cubNq
            for (int n=0;n<p_cubNq;n++) {
              const dfloat Pi = s_cubProjectT[n][i];

#pragma unroll p_NblockAdvVol
	      for(int es=0;es<p_NblockAdvVol;++es){
		r_rhsq[es][k] += Pi*s_rhsq[es][j][n];
	      }
            }
          }
        }
      }
      
      @barrier("local");
    }// k loop
    
    //project in k and write out
    for(int j=0;j<p_cubNq;++j;@inner(1)){
      for(int i=0;i<p_cubNq;++i;@inner(0)){
        if ((i<p_Nq) && (j<p_Nq)) {

#pragma unroll p_Nq
	  for(int k=0;k<p_Nq;++k){
	    
	    dfloat r_rhsqout[p_NblockAdvVol];
#pragma unroll p_NblockAdvVol
	    for(int es=0;es<p_NblockAdvVol;++es){
	      r_rhsqout[es] = 0;
	    }
	    
#pragma unroll p_cubNq
	    for(int n=0;n<p_cubNq;++n){
	      const dfloat Pk = s_cubProjectT[n][k];

#pragma unroll p_NblockAdvVol
	      for(int es=0;es<p_NblockAdvVol;++es){
		r_rhsqout[es] += Pk*r_rhsq[es][n];
	      }
	    }
	    
#pragma unroll p_NblockAdvVol
	    for(int es=0;es<p_NblockAdvVol;++es){
	      
	      const int e = es+eo;
	      if(e<Nelements){
		const dlong gid = e*p_Np*p_Nvgeo+ k*p_Nq*p_Nq + j*p_Nq +i;
		const dfloat invJW = vgeo[gid + p_IJWID*p_Np];
		
		const dlong id = e*p_Np + k*p_Nq*p_Nq + j*p_Nq + i;
		rhsq[id] = -invJW*p_half*r_rhsqout[es];
	      }
	    }
	  }
	}
      }
    }
  }
}


// skew form - with directional approach  (spills to local, a lot)
@kernel void advectionCubatureSkewVolumeHex3D_v2(const dlong Nelements,
					      @restrict const dfloat * vgeo,
					      @restrict const dfloat * cubvgeo,
					      @restrict const dfloat * cubD,
					      @restrict const dfloat * cubInterpT,
					      @restrict const dfloat * cubProjectT,
					      @restrict const dfloat * cubAdvectionVelocityJW,
					      @restrict const dfloat * q,
					      @restrict dfloat * rhsq){

  for(dlong e=0; e<Nelements; ++e; @outer(0)) {
    
    @shared    dfloat s_q[p_Nq][p_Nq][p_Nq];
    @exclusive dfloat r_Iqi[p_cubNq], r_Iqj[p_cubNq], r_Iqk[p_cubNq];
    @exclusive dfloat r_Dqi[p_cubNq], r_Dqj[p_cubNq], r_Dqk[p_cubNq];
    @exclusive dfloat r_PDIqi[p_Nq], r_PDIqj[p_Nq], r_PDIqk[p_Nq];

    @shared    dfloat s_P[p_Nq][p_cubNq], s_D[p_cubNq][p_Nq], s_I[p_cubNq][p_Nq];
    
    for(int j=0;j<p_Nq;++j;@inner(1)){
      for(int i=0;i<p_Nq;++i;@inner(0)){

	//#pragma unroll p_Nq
	for(int k=0;k<p_Nq;++k){
	  dlong base = e*p_Np + k*p_Nq*p_Nq + j*p_Nq + i;
	  s_q[k][j][i] = q[base];
	}

	int n = j*p_Nq + i;
	while(n<p_cubNq*p_Nq){
	  s_P[0][n] = cubProjectT[n];
	  s_I[0][n] = cubInterpT[n];
	  s_D[0][n] = cubD[n];
	  n+=p_Nq*p_Nq;
	}
      }
    }
    
    @barrier("local");
    
    for(int b=0;b<p_Nq;++b;@inner(1)){
      for(int a=0;a<p_Nq;++a;@inner(0)){

	//#pragma unroll p_cubNq
	for(int c=0;c<p_cubNq;++c){
	  r_Iqi[c] = 0; r_Iqj[c] = 0; r_Iqk[c] = 0;
	  r_Dqi[c] = 0; r_Dqj[c] = 0; r_Dqk[c] = 0;
	}

	// Ic*q
	//#pragma unroll p_Nq
	for(int c=0;c<p_Nq;++c){
	  dfloat r_qi = s_q[b][a][c];
	  dfloat r_qj = s_q[b][c][a];
	  dfloat r_qk = s_q[c][b][a];

	  //#pragma unroll p_cubNq
	  for(int n=0;n<p_cubNq;++n){
	    const dfloat Inc = s_I[n][c];// 6 flops for 8 bytes (note transpose)
	    r_Iqi[n] += Inc*r_qi;
	    r_Iqj[n] += Inc*r_qj;
	    r_Iqk[n] += Inc*r_qk;
	  }

	  //#pragma unroll p_cubNq
	  for(int n=0;n<p_cubNq;++n){
	    const dfloat Dnc = s_D[n][c];// 6 flops for 8 bytes (note transpose)
	    r_Dqi[n] += Dnc*r_qi;
	    r_Dqj[n] += Dnc*r_qj;
	    r_Dqk[n] += Dnc*r_qk;
	  }
	}

	// Cc*Jc*wc*Ic*q
	//#pragma unroll p_cubNq
	for(int c=0;c<p_cubNq;++c){
	  const dlong gid = 3*e*p_Nq*p_Nq*p_cubNq + b*p_Nq*p_cubNq+ a*p_cubNq + c;
	  const dfloat cr = cubAdvectionVelocityJW[gid + 0*p_Nq*p_Nq*p_cubNq];
	  const dfloat cs = cubAdvectionVelocityJW[gid + 1*p_Nq*p_Nq*p_cubNq];
	  const dfloat ct = cubAdvectionVelocityJW[gid + 2*p_Nq*p_Nq*p_cubNq];

	  r_Iqi[c] *= cr;
	  r_Iqj[c] *= cs;
	  r_Iqk[c] *= ct;

	  r_Dqi[c] *= cr;
	  r_Dqj[c] *= cs;
	  r_Dqk[c] *= ct;
	}

	//#pragma unroll p_Nq	
	for(int m=0;m<p_Nq;++m){
	  r_PDIqi[m] = 0; r_PDIqj[m] = 0; r_PDIqk[m] = 0;
	}

	// P^t*Dc'*Cc*Jc*wc*Ic*q
	//#pragma unroll p_Nq
	for(int n=0;n<p_Nq;++n){
	  
	  dfloat r_resi = 0, r_resj = 0, r_resk = 0;
	  
	  //#pragma unroll p_cubNq
	  for(int m=0;m<p_cubNq;++m){
	    const dfloat Dmn = s_D[m][n];
	    r_resi += Dmn*r_Iqi[m];
	    r_resj += Dmn*r_Iqj[m];
	    r_resk += Dmn*r_Iqk[m];

	    const dfloat Imn = s_I[m][n];
	    r_resi += Imn*r_Dqi[m];
	    r_resj += Imn*r_Dqj[m];
	    r_resk += Imn*r_Dqk[m];
	  }
	  
	  //#pragma unroll p_cubNq
	  for(int m=0;m<p_cubNq;++m){
	    const dfloat Pnm = s_P[n][m];
	    r_PDIqi[m] += Pnm*r_resi;
	    r_PDIqj[m] += Pnm*r_resj;
	    r_PDIqk[m] += Pnm*r_resk;
	  }
	}

      
	// (1/Jc)*P^t*Dc'*Cc*Jc*wc*Ic*q
	//#pragma unroll p_cubNq
	for(int m=0;m<p_cubNq;++m){
	  // fix this later - will multiply by 1/J
	  const dlong base = 3*e*p_Nq*p_Nq*p_cubNq + b*p_Nq*p_cubNq+ a*p_cubNq + m;
	  
	  r_PDIqi[m] *= cubvgeo[base+0*p_Nq*p_Nq*p_cubNq];
	  r_PDIqj[m] *= cubvgeo[base+1*p_Nq*p_Nq*p_cubNq];
	  r_PDIqk[m] *= cubvgeo[base+2*p_Nq*p_Nq*p_cubNq];
	}

	//#pragma unroll p_Nq
	for(int n=0;n<p_Nq;++n){
	  
	  dfloat r_resqi = 0, r_resqj = 0, r_resqk = 0;
	  
	  //#pragma unroll p_cubNq
	  for(int m=0;m<p_cubNq;++m){
	    // all threads reas the same
	    const dfloat Pmn = s_P[n][m];  // 6 flops for 8 bytes (12*6/8 TFLOPS/s)
	    r_resqi += Pmn*r_PDIqi[m];
	    r_resqj += Pmn*r_PDIqj[m];
	    r_resqk += Pmn*r_PDIqk[m];
	  }
	  
	  r_Iqi[n] = r_resqi;
	  r_Iqj[n] = r_resqj;
	  r_Iqk[n] = r_resqk;
	}
	
      } // end a
    } // end b

    @barrier("local");
    
    for(int b=0;b<p_Nq;++b;@inner(1)){
      for(int a=0;a<p_Nq;++a;@inner(0)){

	//#pragma unroll p_Nq
	for(int c=0;c<p_Nq;++c){
	  s_q[b][a][c] = r_Iqi[c];
	}
      }
    }
    
    @barrier("local");
    
    for(int b=0;b<p_Nq;++b;@inner(1)){
      for(int a=0;a<p_Nq;++a;@inner(0)){

	//#pragma unroll p_Nq
	for(int c=0;c<p_Nq;++c){
	  s_q[b][c][a] += r_Iqj[c];
	}
      }
    }
    
    @barrier("local");

    for(int b=0;b<p_Nq;++b;@inner(1)){
      for(int a=0;a<p_Nq;++a;@inner(0)){

	//#pragma unroll p_Nq
	for(int c=0;c<p_Nq;++c){
	  
	  const dfloat qbca = s_q[c][b][a] + r_Iqk[c];
	  const dlong id = e*p_Np + c*p_Nq*p_Nq +  b*p_Nq + a;
	  
	  rhsq[id] = qbca;
	}
      }
    }
  }
}


// reduced use of local
@kernel void advectionCubatureSkewVolumeHex3D(const dlong Nelements,
					      @restrict const dfloat * vgeo,
					      @restrict const dfloat * cubvgeo,
					      @restrict const dfloat * cubD,
					      @restrict const dfloat * cubInterpT,
					      @restrict const dfloat * cubProjectT,
					      @restrict const dfloat * cubAdvectionVelocityJW,
					      @restrict const dfloat * q,
					      @restrict dfloat * rhsq){

  for(dlong e=0; e<Nelements; ++e; @outer(0)) {
    
    @shared    dfloat s_q[p_Nq][p_Nq][p_Nq];
    @exclusive dfloat r_tmpi[p_Nq], r_tmpj[p_Nq], r_tmpk[p_Nq];
    @exclusive dfloat r_resqi[p_Nq], r_resqj[p_Nq], r_resqk[p_Nq];

    @shared    dfloat s_P[p_Nq][p_cubNq], s_D[p_cubNq][p_Nq], s_I[p_cubNq][p_Nq];
    
    for(int j=0;j<p_Nq;++j;@inner(1)){
      for(int i=0;i<p_Nq;++i;@inner(0)){

	//#pragma unroll p_Nq
	for(int k=0;k<p_Nq;++k){
	  dlong base = e*p_Np + k*p_Nq*p_Nq + j*p_Nq + i;
	  s_q[k][j][i] = q[base];
	}

	int n = j*p_Nq + i;
	while(n<p_cubNq*p_Nq){
	  s_P[0][n] = cubProjectT[n];
	  s_I[0][n] = cubInterpT[n];
	  s_D[0][n] = cubD[n];
	  n+=p_Nq*p_Nq;
	}
      }
    }
    
    @barrier("local");
    
    for(int b=0;b<p_Nq;++b;@inner(1)){
      for(int a=0;a<p_Nq;++a;@inner(0)){

#if 0
    // wrong size
	for(int c=0;c<p_cubNq;++c){
	  r_resqi[c] = s_q[b][a][c]; // this is sloppy (5 reads, 6 flops => 13*6/(5*8) TFLOPS)
	  r_resqj[c] = s_q[b][c][a];
	  r_resqk[c] = s_q[c][b][a];
	}
#endif	
	//#pragma unroll p_Nq
	for(int m=0;m<p_Nq;++m){
	  r_tmpi[m] = 0; r_tmpj[m] = 0; r_tmpk[m] = 0;
	}

	//#pragma unroll p_cubNq
	for(int c=0;c<p_cubNq;++c){
	  
	  dfloat r_Iqi = 0, r_Iqj = 0, r_Iqk = 0;
	  dfloat r_Dqi = 0, r_Dqj = 0, r_Dqk = 0;

	  //#pragma unroll p_Nq
	  for(int n=0;n<p_Nq;++n){
	    const dfloat Icn = s_I[c][n];
	    const dfloat Dcn = s_D[c][n];

#if 1
	    const dfloat r_qi = s_q[b][a][n]; // this is sloppy (5 reads, 6 flops => 13*6/(5*8) TFLOPS)
	    const dfloat r_qj = s_q[b][n][a];
	    const dfloat r_qk = s_q[n][b][a];
#else
	    const dfloat r_qi = r_resqi[n];
	    const dfloat r_qj = r_resqj[n];
	    const dfloat r_qk = r_resqk[n];
#endif
	    
	    r_Iqi += Icn*r_qi;
	    r_Iqj += Icn*r_qj;
	    r_Iqk += Icn*r_qk;

	    r_Dqi += Dcn*r_qi;
	    r_Dqj += Dcn*r_qj;
	    r_Dqk += Dcn*r_qk;
	  }
	  
	  // Cc*Jc*wc*Ic*q
	  const dlong gid = 3*e*p_Nq*p_Nq*p_cubNq + b*p_Nq*p_cubNq+ a*p_cubNq + c;
	  const dfloat cr = cubAdvectionVelocityJW[gid + 0*p_Nq*p_Nq*p_cubNq];
	  const dfloat cs = cubAdvectionVelocityJW[gid + 1*p_Nq*p_Nq*p_cubNq];
	  const dfloat ct = cubAdvectionVelocityJW[gid + 2*p_Nq*p_Nq*p_cubNq];
	  
	  r_Iqi *= cr;
	  r_Iqj *= cs;
	  r_Iqk *= ct;

	  r_Dqi *= cr;
	  r_Dqj *= cs;
	  r_Dqk *= ct;

	  //#pragma unroll p_Nq
	  for(int n=0;n<p_Nq;++n){
	    const dfloat Icn = s_I[c][n];
	    const dfloat Dcn = s_D[c][n];
	    
	    r_tmpi[n] += Icn*r_Dqi;
	    r_tmpj[n] += Icn*r_Dqj;
	    r_tmpk[n] += Icn*r_Dqk;
	    
	    r_tmpi[n] += Dcn*r_Iqi;
	    r_tmpj[n] += Dcn*r_Iqj;
	    r_tmpk[n] += Dcn*r_Iqk;
	  }
	} // end c loop

      }
    } // end b loop
    
    // adding this barrier improves performance ??????????????????//
    @barrier("local");
    
    for(int b=0;b<p_Nq;++b;@inner(1)){
      for(int a=0;a<p_Nq;++a;@inner(0)){
	
	//#pragma unroll p_Nq
	for(int m=0;m<p_Nq;++m){
	  r_resqi[m] = 0; r_resqj[m] = 0; r_resqk[m] = 0;
	}

	//#pragma unroll p_cubNq
	for(int m=0;m<p_cubNq;++m){

	  dfloat r_Pi = 0, r_Pj = 0, r_Pk = 0;
	  
	  //#pragma unroll p_Nq
	  for(int n=0;n<p_Nq;++n){
	    const dfloat Pnm = s_P[n][m];
	    r_Pi += Pnm*r_tmpi[n];
	    r_Pj += Pnm*r_tmpj[n];
	    r_Pk += Pnm*r_tmpk[n];
	  }

	  // fix this later - will multiply by 1/J
	  const dlong base = 3*e*p_Nq*p_Nq*p_cubNq + b*p_Nq*p_cubNq+ a*p_cubNq + m;
	  
	  r_Pi *= cubvgeo[base+0*p_Nq*p_Nq*p_cubNq];
	  r_Pj *= cubvgeo[base+1*p_Nq*p_Nq*p_cubNq];
	  r_Pk *= cubvgeo[base+2*p_Nq*p_Nq*p_cubNq];
	  
	  //#pragma unroll p_Nq
	  for(int n=0;n<p_Nq;++n){
	    
	    // all threads reas the same
	    const dfloat Pmn = s_P[n][m];  // 6 flops for 8 bytes (12*6/8 TFLOPS/s)
	    r_resqi[n] += Pmn*r_Pi;
	    r_resqj[n] += Pmn*r_Pj;
	    r_resqk[n] += Pmn*r_Pk;
	  }
	}
	
      } // end a
    } // end b

    // THIS BARRIER IS IMPORTANT ???
    @barrier("local");
    
    for(int b=0;b<p_Nq;++b;@inner(1)){
      for(int a=0;a<p_Nq;++a;@inner(0)){

	//#pragma unroll p_Nq
	for(int c=0;c<p_Nq;++c){
	  s_q[b][a][c] = r_resqi[c];
	}
      }
    }
    
    @barrier("local");
    
    for(int b=0;b<p_Nq;++b;@inner(1)){
      for(int a=0;a<p_Nq;++a;@inner(0)){
	
	//#pragma unroll p_Nq
	for(int c=0;c<p_Nq;++c){
	  s_q[b][c][a] += r_resqj[c];
	}
      }
    }
    
    @barrier("local");

    for(int b=0;b<p_Nq;++b;@inner(1)){
      for(int a=0;a<p_Nq;++a;@inner(0)){

	//#pragma unroll p_Nq
	for(int c=0;c<p_Nq;++c){
	  
	  const dfloat qbca = s_q[c][b][a] + r_resqk[c];
	  const dlong id = e*p_Np + c*p_Nq*p_Nq +  b*p_Nq + a;
	  
	  rhsq[id] = qbca;
	}
      }
    }
  }
}

