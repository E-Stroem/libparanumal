/*

The MIT License (MIT)

Copyright (c) 2017 Tim Warburton, Noel Chalmers, Jesse Chan, Ali Karakus

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the "Software"), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in all
copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
SOFTWARE.

*/

/* Raises the pressure variable into the velocity space. */
@kernel void stokesRaisePressureHex3D(const dlong Nelements,
                                      @restrict const dfloat *interpRaise,
                                      @restrict const dfloat *p,
                                      @restrict dfloat *pRaised)
{
  /* TODO:  This is really inefficient, but it's going to go away eventually,
   * so we don't care.
   */
  for (int e = 0; e < Nelements; e++; @outer(0)) {

    @shared dfloat s_p[p_NqV][p_NqV];
    @shared dfloat s_p1[p_NqV][p_NqV];
    @shared dfloat s_interpRaise[p_NqV][p_NqP];
    
    @exclusive dfloat r_p[p_NqV];
    
    for (int b = 0; b < p_NqV; b++; @inner(1)) {
      for (int a = 0; a < p_NqV; a++; @inner(0)) {

	if(a<p_NqP)
	  s_interpRaise[b][a] = interpRaise[b*p_NqP+a];
	
	for(int c=0;c<p_NqV;++c){
	  r_p[c] = 0;
	}
      }
    }

    @barrier("local");
    
    for (int b = 0; b < p_NqV; b++; @inner(1)) {
      for (int a = 0; a < p_NqV; a++; @inner(0)) {
	
	if(a<p_NqP && b<p_NqP){
	  for(int c=0;c<p_NqP;++c){
	    dfloat pab = p[e*p_NpP + c*p_NqP*p_NqP + b*p_NqP + a];
	    for(int k=0;k<p_NqV;++k){
	      r_p[k] += s_interpRaise[k][c]*pab;
	    }
	  }
	}
      }
    }

    for(int k=0;k<p_NqV;++k){

      @barrier("local");
      
      for (int b = 0; b < p_NqV; b++; @inner(1)) {
	for (int a = 0; a < p_NqV; a++; @inner(0)) {
	  
	  if(a<p_NqP && b<p_NqP){
	    s_p[b][a] = r_p[k];
	  }
	}
      }

      @barrier("local");
      
      for (int j = 0; j < p_NqV; j++; @inner(1)) {
	for (int a = 0; a < p_NqV; a++; @inner(0)) {
	  
	  if(a<p_NqP){
	    dfloat Ip = 0;
	    
	    for(int b=0;b<p_NqP;++b){
	      Ip += s_interpRaise[j][b]*s_p[b][a];
	    }
	    
	    s_p1[j][a] = Ip;
	  }
	}
      }

      @barrier("local");
      
      for (int j = 0; j < p_NqV; j++; @inner(1)) {
	for (int i = 0; i < p_NqV; i++; @inner(0)) {
	  
	  dfloat Ip = 0;
	  
	  for(int a=0;a<p_NqP;++a){
	    Ip += s_interpRaise[i][a]*s_p1[j][a];
	  }
	  
	  pRaised[e*p_NpV + k*p_NqV*p_NqV + j*p_NqV + i] = Ip;
	}
      }
    }
  }
}
