/*

The MIT License (MIT)

Copyright (c) 2017 Tim Warburton, Noel Chalmers, Jesse Chan, Ali Karakus

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the "Software"), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in all
copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
SOFTWARE.

*/


/* Raises the pressure variable into the velocity space. */
@kernel void stokesLowerPressureHex3D(const dlong Nelements,
                                      @restrict const dfloat *interpRaise,
                                      @restrict const dfloat *pRaised,
                                      @restrict dfloat *p)
{

  for (int e = 0; e < Nelements; e++; @outer(0)) {

    @shared dfloat s_p[p_NqV][p_NqV];
    @shared dfloat s_p1[p_NqV][p_NqV];
    @shared dfloat s_interpRaise[p_NqV][p_NqP];
    
    @exclusive dfloat r_p[p_NqP];
    
    for (int j = 0; j < p_NqV; j++; @inner(1)) {
      for (int i = 0; i < p_NqV; i++; @inner(0)) {
	
	if(i<p_NqP)
	  s_interpRaise[j][i] = interpRaise[j*p_NqP+i];
	
	for(int c=0;c<p_NqP;++c){
	  r_p[c] = 0;
	}
      }
    }

    @barrier("local");
    
    for (int j = 0; j < p_NqV; j++; @inner(1)) {
      for (int i = 0; i < p_NqV; i++; @inner(0)) {
	
	for(int k=0;k<p_NqV;++k){
	  dfloat pij = pRaised[e*p_NpV + k*p_NqV*p_NqV + j*p_NqV + i];
	  for(int c=0;c<p_NqP;++c){
	    r_p[c] += s_interpRaise[k][c]*pij;
	  }
	}
      }
    }
    
    for(int c=0;c<p_NqP;++c){

      @barrier("local");
      
      for (int j = 0; j < p_NqV; j++; @inner(1)) {
	for (int i = 0; i < p_NqV; i++; @inner(0)) {
	  
	  s_p[j][i] = r_p[c];
	}
      }
      
      @barrier("local");
      
      for (int b = 0; b < p_NqV; b++; @inner(1)) {
	for (int i = 0; i < p_NqV; i++; @inner(0)) {
	  
	  if(b<p_NqP){
	    dfloat Ip = 0;
	    
	    for(int j=0;j<p_NqV;++j){
	      Ip += s_interpRaise[j][b]*s_p[j][i];
	    }
	    
	    s_p1[b][i] = Ip;
	  }
	}
      }
      
      @barrier("local");
      
      for (int b = 0; b < p_NqV; b++; @inner(1)) {
	for (int a = 0; a < p_NqV; a++; @inner(0)) {

	  if(a<p_NqP && b<p_NqP){
	    dfloat Ip = 0;
	    
	    for(int i=0;i<p_NqV;++i){
	      Ip += s_interpRaise[i][a]*s_p1[b][i];
	    }
	    
	    p[e*p_NpP + c*p_NqP*p_NqP + b*p_NqP + a] = Ip;
	  }
	}
      }
    }
  }
}



#if 0
/* Lowers a raised pressure variable down to the pressure space by transposing
 * the raising operator.
 */
@kernel void stokesLowerPressureHex3D(const dlong Nelements,
                                      @restrict const dfloat *interpRaise,
                                      @restrict const dfloat *pRaised,
                                      @restrict dfloat *p)
{
  /* TODO:  This is really inefficient, but it's going to go away eventually,
   * so we don't care.
   */
  for (int e = 0; e < Nelements; e++; @outer(0)) {
    for (int i = 0; i < p_NqP; i++; @inner(2)) {
      for (int j = 0; j < p_NqP; j++; @inner(1)) {
        for (int k = 0; k < p_NqP; k++; @inner(0)) {
          const dlong ind = e*p_NpP + i*p_NqP*p_NqP + j*p_NqP + k;
          p[ind] = 0.0;
          for (int ell = 0; ell < p_NqV; ell++) {
            dfloat c = 0.0;
            for (int m = 0; m < p_NqV; m++) {
              dfloat b = 0.0;
              for (int n = 0; n < p_NqV; n++)
                b += interpRaise[n*p_NqP + k]*pRaised[e*p_NpV + ell*p_NqV*p_NqV + m*p_NqV + n];
              c += interpRaise[m*p_NqP + j]*b;
            }
            p[ind] += interpRaise[ell*p_NqP + i]*c;
          }
        }
      }
    }
  }
}

#endif
